buildscript {
    repositories {
        gradlePluginPortal()
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.42.0' // gradle dependencyUpdates -Drevision=release
    id 'java-library'
    id 'idea'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

// Define dependencies
dependencies {
    implementation 'com.hierynomus:sshj:0.33.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

def manifestAttrs = [
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version,
        'Specification-Vendor'  : project.author,
        'Built-By'              : project.author,
        'Specification-Title'   : project.name,
        'Extension-Name'        : project.name,
        'Specification-Version' : project.version
]

jar {
    manifest {
        attributes(manifestAttrs)
    }
}

java {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'com.candil'
            artifactId ='jnc'
            version = '1.3.0'
            pom {
                name = 'JNC (NETCONF Client)'
                description = 'Library that implements a client for interacting with NETCONF servers.'
                url = "https://github.com/candil-data-fabric/JNC"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'Tail-f'
                        name = 'Tail-f Systems'
                    }
                    developer {
                        id = 'candil'
                        name = 'CANDIL Labs'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/candil-data-fabric/jnc"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('javadoc') {
    enabled = false
}
